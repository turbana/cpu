;;
;; Test ISA
;;
; TODO add memory tests
; TODO update reading PC is no longer + 1
;
; final check |@| 348 ($1=F00D $cr0=005A)

	.data
n:	.word	0xABCD
m:	.word	0x0000
	.zero	32
sbp:

	.text
	.ldi	$4, n			; $4 = &n			|@|   5 ($4=0000)
	.ldi	$6, m			; $6 = &m			|@|   7 ($6=0001)
	add	$5, $0, 2		; $5 = 2			|@|   8 ($5=0002)
					;
1:	ldw	$1, 0($4)		; $1 = n			|@|   9 ($1=ABCD)
	stw	0($6), $1		;  m = n			<memory test>
	shl	$2, $1, 8		; $2 = n << 8			|@|  12 ($2=CD00)
	shr	$3, $1, 8		; $3 = n >> 8			|@|  13 ($3=00AB)
	or	$1, $3, $2		; $1 = $3 | $2			|@|  14 ($1=CDAB)
	stw	0($4), $1		;  n = $1			<memory test>
	as.z	$5, $5, -1		; $5 -= 1; loop until 0		|@|  16 ($5=0001)
	jmp	1b			;
					;				|@|  27 ($1=ABCD $2=AB00 $3=00CD $5=0000)
	ldw	$1, 0($4)		; $1 = n (0xABCD)		|@|  29 ($1=ABCD)
	ldw	$2, 0($6)		; $2 = m (0xCDAB)		|@|  30 ($2=CDAB)
	sub	$3, $0, -4		; $3 = 4			|@|  31 ($3=0004)
1:	shl	$1, $1, 1		; $1 <<= 1			|@|  32 ($1=579A)
	shr	$2, $2, 1		; $2 >>= 1			|@|  33 ($2=66D5)
	sub	$3, $3, 1		; $3 -= 1			|@|  34 ($3=0003)
	s.eq	$3, $0			; loop until $3 == 0
	jmp	1b			;
					;				|@|  55 ($1=BCD0 $2=0CDA $3=0000)
	or	$3, $1, $2		; $3 = $1 | $2			|@|  58 ($3=BCDA)
	stw	0($4), $3		;  n = $3			<memory test>
	ldw	$1, $0($4)		; $1 = n			|@|  60 ($1=BCDA)
	ldw	$2, $0($6)		; $2 = m			|@|  61 ($2=CDAB)
	xor	$3, $1, $2		; $3 = 0xBCDA ^ 0xCDAB = 0x7171	|@|  63 ($3=7171)
	shl	$2, $3, 1		; $2 = $3 << 1 = 0xE2E2		|@|  64 ($2=E2E2)
	lui	$1, 0x80		; $1 = 0x8000			|@|  65 ($1=8000)
	or	$1, $1, $3		; $1 = $1 | $3 = 0xF171		|@|  66 ($1=F171)
	xor	$3, $3, $3		; $3 = $3 ^ $3 = 0		|@|  67 ($3=0000)
	sub	$3, $0, 1		; $3 = -1			|@|  68 ($3=FFFF)
	stw	0($6), $3		;  m = $3			<memory test>
	add	$3, $0, -1		; $3 = 0xFFFF			|@|  70 ($3=FFFF)
	shr	$3, $3, 8		; $3 >>= 8 = 0x00FF		|@|  71 ($3=00FF)
	lui	$2, 0xFF		; $2 = 0xFF00			|@|  72 ($2=FF00)
	sar	$2, $2, 8		; $2 >>>= 8 = 0xFFFF		|@|  73 ($2=FFFF)
	and	$1, $1, $2		; $1 = $1 & $2 = 0xF171		|@|  74 ($1=F171)
					;
	.ldi	$7, sbp			; setup stack			|@|  76 ($7=0022)
	.push	$1			; push 0xF171			|@|  77 ($7=0021)
					;				<memory test>
	sub	$7, $7, 1		; .call count_bits		|@|  79 ($7=0020)
	lcr	$5, $cr0		;				|@|  80 ($5=002A)
	add	$5, $5, 4		;				|@|  81 ($5=002E)
	stw	0($7), $5		;				<memory test>
	jmp	count_bits		;
	.pop	$2			; $2 = result = 9		|@| 238 ($2=0009)
					;				|@| 239 ($7=0022)
	xor	$3, $1, $2		; $3 = $1 ^ $2 = 0xF178		|@| 240 ($3=F178)
					;
	.ldi	$1, test		; $1 = &test			|@| 242 ($1=0035)
	lcr	$2, $cr0		; 				|@| 244 ($2=0033)
	add	$2, $2, 2		; $2 = &test			|@| 244 ($2=0035)
test:	s.eq	$1, $2			; skip if same address
	add	$3, $3, $1		; scramble $3
					;				|@| 247 ($3=F178)
	shr	$3, $3, 1		; $3 >>= 1 = 0x78BC		|@| 247 ($3=78BC)
	.ldi	$5, 0x8000		; $5 = 0x8000			|@| 248 ($5=8000)
	or	$3, $3, $5		; $3 |= $5 = 0xF8BC		|@| 249 ($3=F8BC)
					;
	add	$3, $3, 1		; $3 += 1 (0xF8BD)		|@| 250 ($3=F8BD)
	ldw	$2, 0($6)		; $2 = m (0xFFFF)		|@| 251 ($2=FFFF)
	ldw	$1, 0($6)		; $1 = m (0xFFFF)		|@| 252 ($1=FFFF)
	.ldi	$5, 12			; $5 = 12 (shift count)		|@| 254 ($5=000C)
1:	shl	$1, $1, 1		; $1 <<= 1			|@| 255 ($1=FFFE)
	shr	$2, $2, 1		; $2 >>= 1			|@| 256 ($2=7FFF)
	as.z	$5, $5, -1		; dec 5, skip if zero		|@| 257 ($5=000B)
	jmp	1b			; loop
					;				|@| 323 ($1=F000 $2=000F $3=F8BD $5=0000)
	or	$1, $1, $2		; $1 = $1 | $2 (0xF00F)		|@| 325 ($1=F00F)
	and	$1, $1, $3		; $1 = $3 & $1 = 0xF00D		|@| 326 ($1=F00D)
					;
	add	$2, $0, 2		; $2 = 2			|@| 327 ($2=0002)
	add	$3, $2, 1		; $3 = 3			|@| 328 ($3=0003)
	add	$4, $3, 1		; $4 = 4			|@| 329 ($4=0004)
	add	$5, $4, 1		; $5 = 5			|@| 330 ($5=0005)
	lui	$6, 0x01		; $6 = 0x0100			|@| 331 ($6=0100)
	addi	$6, 0xFF		; $6 = 0x00FF			|@| 332 ($6=00FF)
	sex	$6, $6			; $6 = 0xFFFF (-1)		|@| 333 ($6=FFFF)
	add	$7, $0, 1		; $7 = 1			|@| 334 ($7=0001)
					;
	s.ne	$2, $3			; test skips			|@| 336 ($1=F00D)
	add	$1, $1, 1		; we shouldn't hit any adds
	s.gt	$7, $6			;				|@| 338 ($1=F00D)
	add	$1, $1, 1		;
	s.gte	$2, $7			;				|@| 340 ($1=F00D)
	add	$1, $1, 1		;
	s.lt	$4, $5			;				|@| 342 ($1=F00D)
	add	$1, $1, 1		;
	s.lte	$6, $2			;				|@| 344 ($1=F00D)
	add	$1, $1, 1		;
	s.ult	$7, $6			;				|@| 346 ($1=F00D)
	add	$1, $1, 1		;
	s.ulte	$7, $2			;				|@| 348 ($1=F00D)
	add	$1, $1, 1		;
					;
	halt				;
					;
count_bits:				;				|@|  86 ($1=F171)
	.push	$1			; preserve $1			|@|  86 ($7=001F)
					;				<memory test>
	ldw	$1, 2($7)		; $1 = argument = 0xF171	|@|  88 ($1=F171)
	.push	$2			; preserve $2			|@|  89 ($7=001E)
					;				<memory test>
	.push	$3			; preserve $3			|@|  91 ($7=001D)
					;				<memory test>
	.push	$4			; preserve $4			|@|  93 ($7=001C)
					;				<memory test>
	add	$2, $0, $0		; $2 = 0			|@|  95 ($2=0000)
	.ldi	$4, 16			; $4 = 16 (bits)		|@|  97 ($4=0010)
1:	and	$3, $1, 1		; $3 = $1 & 1			|@|  98 ($3=0001)
	s.eq	$3, $0			; skip if 0
	add	$2, $2, 1		; inc $2			|@| 100 ($2=0001)
	shr	$1, $1, 1		; $1 = $1 >> 1			|@| 101 ($1=78B8)
	as.z	$4, $4, -1		; dec $4, skip when done	|@| 102 ($4=000F)
	jmp	1b			; loop				|@| 222 ($1=0000 $2=0009 $3=0001 $4=0000)
	.pop	$4			; restore $4			|@| 224 ($4=0000)
					;				|@| 225 ($7=001D)
	stw	4($7), $2		; result = $2 = 9		<memory test>
	.pop	$3			; restore $3			|@| 227 ($3=00FF)
					;				|@| 228 ($7=001E)
	.pop	$2			; restore $2			|@| 229 ($2=FFFF)
					;				|@| 230 ($7=001F)
	.pop	$1			; restore $1			|@| 231	($1=F171)
					;				|@| 232 ($7=0020)
	.pop	$5			; .ret				|@| 233 ($5=002E)
					;				|@| 234 ($7=0021)
	jmp	$0($5)			;
